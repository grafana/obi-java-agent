plugins {
    id 'java'
    id("com.gradleup.shadow") version '8.3.8'
}

group = 'io.opentelemetry.obi'
version = '0.1.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.bytebuddy:byte-buddy:1.17.6'
    implementation 'net.bytebuddy:byte-buddy-agent:1.17.6'
    implementation 'net.java.dev.jna:jna:5.17.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.3'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.10.2'
    testImplementation 'org.awaitility:awaitility:4.1.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.3'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName.set('agent')
    archiveVersion.set('0.1.0')
    archiveClassifier.set('shaded')
    manifest {
        attributes(
                'Premain-Class': 'io.opentelemetry.obi.java.Agent',
                'Agent-Class': 'io.opentelemetry.obi.java.Agent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Main-Class': 'io.opentelemetry.obi.java.Agent'
        )
    }
    relocate 'com.github', 'io.opentelemetry.obi.com.github'
    relocate 'net.bytebuddy', 'io.opentelemetry.obi.net.bytebuddy'
    // Exclude META-INF files as in Maven Shade plugin
    exclude 'META-INF/**'
    exclude 'META-INF/versions/9/module-info.class'
}
